# cython: language_level=3, boundscheck=False, wraparound=False, cdivision=True, initializedcheck=False
# Same algorithm as py/nbody.py. Semantics that differ from CPython: float division does not raise
# ZeroDivisionError (cdivision), no bounds checks (arrays are C arrays), loop counters are C long.
from libc.math cimport sqrt
import sys

cdef double PI = 3.141592653589793
cdef double SOLAR_MASS = 4 * PI * PI
cdef double DAYS_PER_YEAR = 365.24


cdef void offset_momentum(double* vx, double* vy, double* vz, double* mass) noexcept nogil:
    cdef double px = 0.0, py = 0.0, pz = 0.0
    cdef int i
    for i in range(5):
        px += vx[i] * mass[i]
        py += vy[i] * mass[i]
        pz += vz[i] * mass[i]
    vx[0] = -px / SOLAR_MASS
    vy[0] = -py / SOLAR_MASS
    vz[0] = -pz / SOLAR_MASS


cdef void advance(double dt, long steps, double* x, double* y, double* z,
                  double* vx, double* vy, double* vz, double* mass) noexcept nogil:
    cdef long s
    cdef int i, j
    cdef double dx, dy, dz, d2, mag
    for s in range(steps):
        for i in range(5):
            for j in range(i + 1, 5):
                dx = x[i] - x[j]
                dy = y[i] - y[j]
                dz = z[i] - z[j]
                d2 = dx * dx + dy * dy + dz * dz
                mag = dt / (d2 * sqrt(d2))
                vx[i] -= dx * mass[j] * mag
                vy[i] -= dy * mass[j] * mag
                vz[i] -= dz * mass[j] * mag
                vx[j] += dx * mass[i] * mag
                vy[j] += dy * mass[i] * mag
                vz[j] += dz * mass[i] * mag
        for i in range(5):
            x[i] += dt * vx[i]
            y[i] += dt * vy[i]
            z[i] += dt * vz[i]


cdef double energy(double* x, double* y, double* z, double* vx, double* vy, double* vz,
                   double* mass) noexcept nogil:
    cdef double e = 0.0, dx, dy, dz
    cdef int i, j
    for i in range(5):
        e += 0.5 * mass[i] * (vx[i] * vx[i] + vy[i] * vy[i] + vz[i] * vz[i])
        for j in range(i + 1, 5):
            dx = x[i] - x[j]
            dy = y[i] - y[j]
            dz = z[i] - z[j]
            e -= mass[i] * mass[j] / sqrt(dx * dx + dy * dy + dz * dz)
    return e


def main():
    cdef long n = int(sys.argv[1])
    cdef double[5] x = [0.0, 4.84143144246472090e+00, 8.34336671824457987e+00, 1.28943695621391310e+01, 1.53796971148509165e+01]
    cdef double[5] y = [0.0, -1.16032004402742839e+00, 4.12479856412430479e+00, -1.51111514016986312e+01, -2.59193146099879641e+01]
    cdef double[5] z = [0.0, -1.03622044471123109e-01, -4.03523417114321381e-01, -2.23307578892655734e-01, 1.79258772950371181e-01]
    cdef double[5] vx = [0.0, 1.66007664274403694e-03 * DAYS_PER_YEAR, -2.76742510726862411e-03 * DAYS_PER_YEAR,
                         2.96460137564761618e-03 * DAYS_PER_YEAR, 2.68067772490389322e-03 * DAYS_PER_YEAR]
    cdef double[5] vy = [0.0, 7.69901118419740425e-03 * DAYS_PER_YEAR, 4.99852801234917238e-03 * DAYS_PER_YEAR,
                         2.37847173959480950e-03 * DAYS_PER_YEAR, 1.62824170038242295e-03 * DAYS_PER_YEAR]
    cdef double[5] vz = [0.0, -6.90460016972063023e-05 * DAYS_PER_YEAR, 2.30417297573763929e-05 * DAYS_PER_YEAR,
                         -2.96589568540237556e-05 * DAYS_PER_YEAR, -9.51592254519715870e-05 * DAYS_PER_YEAR]
    cdef double[5] mass = [SOLAR_MASS, 9.54791938424326609e-04 * SOLAR_MASS, 2.85885980666130812e-04 * SOLAR_MASS,
                           4.36624404335156298e-05 * SOLAR_MASS, 5.15138902046611451e-05 * SOLAR_MASS]
    offset_momentum(vx, vy, vz, mass)
    cdef double before = energy(x, y, z, vx, vy, vz, mass)
    advance(0.01, n, x, y, z, vx, vy, vz, mass)
    cdef double after = energy(x, y, z, vx, vy, vz, mass)
    print(f"{before:.9f} {after:.9f}")
